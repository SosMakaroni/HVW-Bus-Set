// --------------------------------------------------------------------------------MB_Citaro2_c_a_VBSH

spriteset(ss_MB_Citaro2_c_a_VBSH_purchase, "src/MB/MB_Citaro2_c_a_VBSH_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű

spriteset(ss_MB_Citaro2_c_a_VBSH, "src/MB/MB_Citaro2_c_a_VBSH_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_MB_Citaro2_c_a_VBSH, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MB/MB_Citaro2_c_a_VBSH_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MB_Citaro2_c_a_VBSH, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MB/MB_Citaro2_c_a_VBSH_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MB_Citaro2_c_a_VBSHi, "src/MB/MB_Citaro2_c_a_VBSH_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_MB_Citaro2_c_a_VBSH {	loaded:  [ss_MB_Citaro2_c_a_VBSH];	loading: [ss_MB_Citaro2_c_a_VBSH];}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sw1_MB_Citaro2_c_a_VBSH_info, position_in_consist ) {
    1: ss_toldat;
    ss_MB_Citaro2_c_a_VBSHi;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sw1_MB_Citaro2_c_a_VBSH, position_in_consist ) {
    1: ss_toldat;
    sg_MB_Citaro2_c_a_VBSH;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_MB_Citaro2_c_a_VBSH, (extra_callback_info1 & 0x12)) {
   0x10: sw1_MB_Citaro2_c_a_VBSH_info; // Depó nézet
   0x11: sw1_MB_Citaro2_c_a_VBSH_info; // Jármű infó/Refit nézet
   0x12: sw1_MB_Citaro2_c_a_VBSH_info; // Jármű lista nézet
   0x00: sw1_MB_Citaro2_c_a_VBSH; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_MB_Citaro2_c_a_VBSH_articulated, extra_callback_info1) {
    0: item_MB_Citaro2_c_a_VBSH;
    1: item_MB_Citaro2_c_a_VBSH_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_MB_Citaro2_c_a_VBSH_length, position_in_consist) {
	1: return 8;
    return 5;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_MB_Citaro2_c_a_VBSH_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_diesel_3door_BUY);
    2: return string(STR_VBSH_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_MB_Citaro2_c_a_VBSH_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_MB_Citaro2_c_a_INFO); // Jármű infóban név
    0x20: sw_MB_Citaro2_c_a_VBSH_namep; // Vásárlási lista név
	0x21: return string(STR_MB_Citaro2_c_a_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_MB_Citaro2_c_a_VBSH) {
	property {
        name:							string(STR_MB_Citaro2_c_a_NAME);
        climates_available:			bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(2012,01,01);
        model_life:					25;
        vehicle_life:					25;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					35;
        sprite_id:						SPRITE_ID_NEW_ROADVEH;
        speed:							80 km/h;
        misc_flags:					bitmask(/*ROADVEH_FLAG_2CC, */ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:					0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:							260 kW;
        weight:							12 ton;
        cargo_capacity:				106;
		cargo_age_period:				185;
        sound_effect:					SOUND_DEPARTURE_MODERN_BUS;
		variant_group:					item_MB_Citaro2_c_a_cc1;
    }
    graphics {
		default:                 		sw_MB_Citaro2_c_a_VBSH;
		purchase:						ss_MB_Citaro2_c_a_VBSH_purchase;
		articulated_part:        		sw_MB_Citaro2_c_a_VBSH_articulated;
		length:                  		sw_MB_Citaro2_c_a_VBSH_length;
		cost_factor:					26 * parapuco;
		running_cost_factor:			39 * pararuco;
		additional_text: 				string(STR_MB_Citaro2_c_a_ADAT, string(STR_SweDuke_ADAT));
		name:							sw_MB_Citaro2_c_a_VBSH_name;
    }
}
item(FEAT_ROADVEHS, item_MB_Citaro2_c_a_VBSH_T) {
	property {
        name:							string(STR_BUG);
        climates_available:			bitmask(NO_CLIMATE);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:					0;
        running_cost_factor:			0;
        sprite_id:						SPRITE_ID_NEW_ROADVEH;
        misc_flags:					bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:					0;
        running_cost_base:				RUNNING_COST_NONE;
        power:							0 kW;
        weight:							0 ton;
        cargo_capacity:				0;
		cargo_age_period:				0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_MB_Citaro2_c_a_VBSH_length;
    }
}

// --------------------------------------------------------------------------------MB_Citaro2_c_a_VBSH End
// --
// --
