
// --------------------------------------------------------------------------------Csepel_Union_a_CC3

spriteset(ss_Csepel_Union_a_CC3_purchase_k, "src/Other/Csepel_Union_a_CC3_8bpp.png")		{tmpl_purchase_k()} // Vásárlási nézet
spriteset(ss_Csepel_Union_a_CC3_purchase_n, "src/Other/Csepel_Union_a_CC3_8bpp.png")		{tmpl_purchase_n()} // Vásárlási nézet

switch (FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3_purchase, param_ptemplate ) {
	1:	ss_Csepel_Union_a_CC3_purchase_n;
	ss_Csepel_Union_a_CC3_purchase_k;
}


spriteset(ss_Csepel_Union_a_CC3, "src/Other/Csepel_Union_a_CC3_8bpp.png")		{tmpl_1()}
alternative_sprites(ss_Csepel_Union_a_CC3, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/Other/Csepel_Union_a_CC3_8bpp.png") {tmpl_2()}
alternative_sprites(ss_Csepel_Union_a_CC3, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/Other/Csepel_Union_a_CC3_8bpp.png") {tmpl_4()}

spriteset(ss_Csepel_Union_a_CC3_info, "src/Other/Csepel_Union_a_CC3_8bpp.png")		{tmpl_depo()} // Depo, járműlista, info nézet

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sw1_Csepel_Union_a_CC3_info, position_in_consist ) {
	1: ss_toldat;
	ss_Csepel_Union_a_CC3_info;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sw1_Csepel_Union_a_CC3, position_in_consist ) {
	1: ss_toldat;
	ss_Csepel_Union_a_CC3;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3, (extra_callback_info1 & 0x12)) {
   0x10: sw1_Csepel_Union_a_CC3_info; // Depó nézet
   0x11: sw1_Csepel_Union_a_CC3_info; // Jármű infó/Refit nézet
   0x12: sw1_Csepel_Union_a_CC3_info; // Jármű lista nézet
   0x00: sw1_Csepel_Union_a_CC3; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3_articulated, extra_callback_info1) {
    0: item_Csepel_Union_a_CC3;
	1: item_Csepel_Union_a_CC3_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3_length, position_in_consist) {
	1: return 5;
    return 8;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_Csepel_Union_a_CC3_NAME2);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_Csepel_Union_a_CC3_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_Csepel_Union_a_CC3_INFO); // Jármű infóban név
    0x20: sw_Csepel_Union_a_CC3_namep; // Vásárlási lista név
	0x21: return string(STR_Csepel_Union_a_CC3_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}

// Item
item(FEAT_ROADVEHS, item_Csepel_Union_a_CC3) {
    property { 
        name:                           string(STR_Csepel_Union_a_CC3_NAME);
        climates_available:         bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:              date(1991,01,01);
        model_life:                 20;
        vehicle_life:                   25;
        reliability_decay:              20;
		cargo_allow_refit:      		[PASS,TOUR];
        loading_speed:                  8;
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;
        speed:                          110 km/h;
        misc_flags:                 bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:                 0;
        running_cost_base:              RUNNING_COST_ROADVEH;
        power:                          213 kW;
        weight:                         11 ton;
        cargo_capacity:             48;
        cargo_age_period:               170;
        sound_effect:                   SOUND_DEPARTURE_MODERN_BUS;
      variant_group:				item_Csepel_Union_a_CC1;
    }
    graphics {
        default:                        sw_Csepel_Union_a_CC3;
        purchase:                       sw_Csepel_Union_a_CC3_purchase;
        articulated_part:               sw_Csepel_Union_a_CC3_articulated;
        length:                         sw_Csepel_Union_a_CC3_length;
        cost_factor:                    20 * parapuco;
        running_cost_factor:            31 * pararuco;
        additional_text:                string(STR_Csepel_Union_a_CC3_DATA);
        name:                           sw_Csepel_Union_a_CC3_name;
    }
}
item(FEAT_ROADVEHS, item_Csepel_Union_a_CC3_T) {
	property {
        name:							string(STR_BUG);
        climates_available:			bitmask(NO_CLIMATE);
        introduction_date:				date(4999999,01,01);
		cargo_allow_refit:				[PASS,TOUR];
        loading_speed:					0;
        cost_factor:					0;
        running_cost_factor:			0;
        sprite_id:						SPRITE_ID_NEW_ROADVEH;
        misc_flags:					bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:					0;
        running_cost_base:				RUNNING_COST_NONE;
        power:							0 kW;
        weight:							0 ton;
        cargo_capacity:				0;
		cargo_age_period:				0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_Csepel_Union_a_CC3_length;
    }
}

// ----------------------------------------------------------------End-------------Csepel_Union_a_CC3
